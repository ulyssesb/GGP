(role white)
(role black)
(init (location 2 1 white))
(init (location 5 1 white))
(init (location 1 2 white))
(init (location 6 2 white))
(init (location 1 5 black))
(init (location 6 5 black))
(init (location 2 6 black))
(init (location 5 6 black))
(init (control white))
(<= (legal ?player noop)
    (role ?player)
    (not (true (control ?player))))
(<= (legal ?player (play ?x1 ?y1 ?x2 ?y2 ?x3 ?y3))
    (true (control ?player))
    (true (location ?x1 ?y1 ?player))
    (queenmove ?x1 ?y1 ?x2 ?y2)
    (emptycell ?x2 ?y2)
    (queenmoveignoring ?x1 ?y1 ?x2 ?y2 ?x3 ?y3)
    (emptycell ?x3 ?y3))
(<= (legal ?player (play ?x1 ?y1 ?x2 ?y2 ?x1 ?y1))
    (true (control ?player))
    (true (location ?x1 ?y1 ?player))
    (queenmove ?x1 ?y1 ?x2 ?y2)
    (emptycell ?x2 ?y2))
(<= (next (location ?x ?y ?state))
    (true (location ?x ?y ?state))
    (does ?player (play ?x1 ?y1 ?x2 ?y2 ?x3 ?y3))
    (distinctcell ?x ?y ?x1 ?y1))
(<= (next (location ?x2 ?y2 ?player))
    (does ?player (play ?x1 ?y1 ?x2 ?y2 ?x3 ?y3)))
(<= (next (location ?x3 ?y3 arrow))
    (does ?player (play ?x1 ?y1 ?x2 ?y2 ?x3 ?y3)))
(<= (next (control black))
    (true (control white)))
(<= (next (control white))
    (true (control black)))
(<= terminal
    (not (legalmove white)))
(<= terminal
    (not (legalmove black)))
(<= (goal white 100)
    (not (legalmove black)))
(<= (goal white 0)
    (legalmove black))
(<= (goal black 100)
    (not (legalmove white)))
(<= (goal black 0)
    (legalmove white))
(<= (legalmove ?player)
    (true (location ?x1 ?y1 ?player))
    (queenmove ?x1 ?y1 ?xany ?yany)
    (emptycell ?xany ?yany))
(<= (queenmove ?x1 ?y1 ?x2 ?y2)
    (rookmove ?x1 ?y1 ?x2 ?y2))
(<= (queenmove ?x1 ?y1 ?x2 ?y2)
    (bishopmove ?x1 ?y1 ?x2 ?y2))
(<= (rookmove ?x1 ?y1 ?x2 ?y2)
    (horizontalmove ?x1 ?y1 ?x2 ?y2))
(<= (rookmove ?x1 ?y1 ?x2 ?y2)
    (horizontalmove ?x2 ?y2 ?x1 ?y1))
(<= (rookmove ?x1 ?y1 ?x2 ?y2)
    (verticalmove ?x1 ?y1 ?x2 ?y2))
(<= (rookmove ?x1 ?y1 ?x2 ?y2)
    (verticalmove ?x2 ?y2 ?x1 ?y1))
(<= (bishopmove ?x1 ?y1 ?x2 ?y2)
    (diagonalupmove ?x1 ?y1 ?x2 ?y2))
(<= (bishopmove ?x1 ?y1 ?x2 ?y2)
    (diagonalupmove ?x2 ?y2 ?x1 ?y1))
(<= (bishopmove ?x1 ?y1 ?x2 ?y2)
    (diagonaldownmove ?x1 ?y1 ?x2 ?y2))
(<= (bishopmove ?x1 ?y1 ?x2 ?y2)
    (diagonaldownmove ?x2 ?y2 ?x1 ?y1))
(<= (horizontalmove ?x1 ?y ?x3 ?y)
    (plusplus ?x1 ?x2)
    (emptycell ?x2 ?y)
    (horizontalmove ?x2 ?y ?x3 ?y)
    (index ?y))
(<= (horizontalmove ?x1 ?y ?x2 ?y)
    (plusplus ?x1 ?x2)
    (index ?y))
(<= (verticalmove ?x ?y1 ?x ?y3)
    (plusplus ?y1 ?y2)
    (emptycell ?x ?y2)
    (verticalmove ?x ?y2 ?x ?y3)
    (index ?x))
(<= (verticalmove ?x ?y1 ?x ?y2)
    (plusplus ?y1 ?y2)
    (index ?x))
(<= (diagonalupmove ?x1 ?y1 ?x3 ?y3)
    (plus 1 ?x1 ?x2)
    (plusplus ?y1 ?y2)
    (emptycell ?x2 ?y2)
    (diagonalupmove ?x2 ?y2 ?x3 ?y3))
(<= (diagonalupmove ?x1 ?y1 ?x2 ?y2)
    (plusplus ?x1 ?x2)
    (plusplus ?y1 ?y2))
(<= (diagonaldownmove ?x1 ?y1 ?x3 ?y3)
    (plusplus ?x1 ?x2)
    (plusplus ?y2 ?y1)
    (emptycell ?x2 ?y2)
    (diagonaldownmove ?x2 ?y2 ?x3 ?y3))
(<= (diagonaldownmove ?x1 ?y1 ?x2 ?y2)
    (plusplus ?x1 ?x2)
    (plusplus ?y2 ?y1))
(<= (queenmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (rookmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2))
(<= (queenmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (bishopmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2))
(<= (rookmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (horizontalmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2))
(<= (rookmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (horizontalmoveignoring ?xig ?yig ?x2 ?y2 ?x1 ?y1))
(<= (rookmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (verticalmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2))
(<= (rookmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (verticalmoveignoring ?xig ?yig ?x2 ?y2 ?x1 ?y1))
(<= (bishopmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (diagonalupmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2))
(<= (bishopmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (diagonalupmoveignoring ?xig ?yig ?x2 ?y2 ?x1 ?y1))
(<= (bishopmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (diagonaldownmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2))
(<= (bishopmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (diagonaldownmoveignoring ?xig ?yig ?x2 ?y2 ?x1 ?y1))
(<= (horizontalmoveignoring ?xig ?yig ?x1 ?y ?x3 ?y)
    (plusplus ?x1 ?x2)
    (emptycell ?x2 ?y)
    (horizontalmoveignoring ?xig ?yig ?x2 ?y ?x3 ?y)
    (index ?y))
(<= (horizontalmoveignoring ?xig ?yig ?x1 ?y ?x3 ?y)
    (plusplus ?x1 ?x2)
    (identicalcell ?xig ?yig ?x2 ?y)
    (horizontalmoveignoring ?xig ?yig ?x2 ?y ?x3 ?y)
    (index ?y))
(<= (horizontalmoveignoring ?xig ?yig ?x1 ?y ?x2 ?y)
    (plusplus ?x1 ?x2)
    (index ?y)
    (cell ?xig ?yig))
(<= (verticalmoveignoring ?xig ?yig ?x ?y1 ?x ?y3)
    (plusplus ?y1 ?y2)
    (emptycell ?x ?y2)
    (verticalmoveignoring ?xig ?yig ?x ?y2 ?x ?y3)
    (index ?x))
(<= (verticalmoveignoring ?xig ?yig ?x ?y1 ?x ?y3)
    (plusplus ?y1 ?y2)
    (identicalcell ?xig ?yig ?x ?y2)
    (verticalmoveignoring ?xig ?yig ?x ?y2 ?x ?y3)
    (index ?x))
(<= (verticalmoveignoring ?xig ?yig ?x ?y1 ?x ?y2)
    (plusplus ?y1 ?y2)
    (index ?x)
    (cell ?xig ?yig))
(<= (diagonalupmoveignoring ?xig ?yig ?x1 ?y1 ?x3 ?y3)
    (plus 1 ?x1 ?x2)
    (plusplus ?y1 ?y2)
    (emptycell ?x2 ?y2)
    (diagonalupmoveignoring ?xig ?yig ?x2 ?y2 ?x3 ?y3))
(<= (diagonalupmoveignoring ?xig ?yig ?x1 ?y1 ?x3 ?y3)
    (plus 1 ?x1 ?x2)
    (plusplus ?y1 ?y2)
    (identicalcell ?xig ?yig ?x2 ?y2)
    (diagonalupmoveignoring ?xig ?yig ?x2 ?y2 ?x3 ?y3))
(<= (diagonalupmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (plusplus ?x1 ?x2)
    (plusplus ?y1 ?y2)
    (cell ?xig ?yig))
(<= (diagonaldownmoveignoring ?xig ?yig ?x1 ?y1 ?x3 ?y3)
    (plusplus ?x1 ?x2)
    (plusplus ?y2 ?y1)
    (emptycell ?x2 ?y2)
    (diagonaldownmoveignoring ?xig ?yig ?x2 ?y2 ?x3 ?y3))
(<= (diagonaldownmoveignoring ?xig ?yig ?x1 ?y1 ?x3 ?y3)
    (plusplus ?x1 ?x2)
    (plusplus ?y2 ?y1)
    (identicalcell ?xig ?yig ?x2 ?y2)
    (diagonaldownmoveignoring ?xig ?yig ?x2 ?y2 ?x3 ?y3))
(<= (diagonaldownmoveignoring ?xig ?yig ?x1 ?y1 ?x2 ?y2)
    (plusplus ?x1 ?x2)
    (plusplus ?y2 ?y1)
    (cell ?xig ?yig))
(<= (emptycell ?x ?y)
    (cell ?x ?y)
    (not (true (location ?x ?y white)))
    (not (true (location ?x ?y black)))
    (not (true (location ?x ?y arrow))))
(<= (identicalcell ?x1 ?y1 ?x2 ?y2)
    (cell ?x1 ?y1)
    (cell ?x2 ?y2)
    (not (distinct ?x1 ?x2))
    (not (distinct ?y1 ?y2)))
(<= (distinctcell ?x1 ?y1 ?x2 ?y2)
    (cell ?x1 ?y1)
    (cell ?x2 ?y2)
    (distinct ?x1 ?x2))
(<= (distinctcell ?x1 ?y1 ?x2 ?y2)
    (cell ?x1 ?y1)
    (cell ?x2 ?y2)
    (distinct ?y1 ?y2))
(<= (cell ?x ?y)
    (index ?x)
    (index ?y))
(index 1)
(index 2)
(index 3)
(index 4)
(index 5)
(index 6)
(plusplus 1 2)
(plusplus 2 3)
(plusplus 3 4)
(plusplus 4 5)
(plusplus 5 6)
